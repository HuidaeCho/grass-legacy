#!/bin/csh
# configure script for nviz

# Revisions:
#  $Id: configure,v 1.14 2000-10-11 05:27:29 cho Exp $

set NV_VER=nviz2.2
set NVWISH_VER=NVWISH2.2
set GMAKE_VER=gmake5

# First print out some introductory instructions
echo This script creates a Gmakefile to build the nviz application.
echo Note that nviz requires Grass5.0 to compile and run correctly.
echo In addition, nviz requires Tcl/Tk 8.x.
echo Note that earlier versions of Tk and Tcl WILL NOT work.
echo
echo Any queries generated by this script will have default settings
echo which are indicated by brackets \(i.e. \[\]\).  You may either enter
echo a value or just press return to use the default setting.
echo
echo -n Press return to continue:
set select=$<

set TOP=$cwd

# The first thing we configure for is the src Gmakefile
# Figure out what version of IRIX we are using
#if ("`uname -s`" != "IRIX") then
#  echo This version of nviz only runs on SGIs running IRIX 
#  exit
#endif

set OS=`uname -s`

# Figure out what gmake to use
echo -n Command used to make current version of GRASS \[$GMAKE_VER\]:
set found=$<
if ("$found" != "") then
  set GMAKE_VER=$found
endif
set GISBASE=`$GMAKE_VER config | sed -e '$ n; d'`

# Figure out where to install the nviz scripts
set install_dir=$GISBASE/etc/$NV_VER
echo -n Directory where run-time scripts are located \[$install_dir\]:
set found=$<
if ("$found" != "") then
  set install_dir=$found
endif

# Check for PostgreSQL
set PQ_OBJS=(openvect.o getCat.o buildInfxQry.o runInfxQry.o)
set PQLIB=(-lpq)
'cp' src/query_postgr_orig.c src/query_postgr.c
'cp' scripts/panel_query_pg.tcl scripts/panel_query.tcl
echo -n Do you want Postgres support \[y\/n\]:
set found=$<
if ("$found" != "y") then
  echo -n Postgres support disabled.
  set PQ_OBJS=''
  set PQLIB=''
  'rm' -rf src/query_postgr.c
  'rm' -rf scripts/panel_query.tcl
  'cp' src/query_postgr_dummy.c src/query_postgr.c
  'cp' scripts/panel_query_orig.tcl scripts/panel_query.tcl
endif

# Now create the makefile
echo ""
echo Creating src/Gmakefile...
set PQ_OBJS=`echo "$PQ_OBJS" | sed 's/\//\\\//g'`
set PQLIB=`echo "$PQLIB" | sed 's/\//\\\//g'`
'rm' -rf src/Gmakefile
sed -e "\
s/PQ_OBJS=sed/PQ_OBJS=${PQ_OBJS}/g;\
s/PQLIB=sed/PQLIB=${PQLIB}/g;\
s/NVWISH_VER=sed/NVWISH_VER=${NVWISH_VER}/g;\
" src/Gmakefile.in > src/Gmakefile

# Finally create the toplevel makefile
echo Creating top-level makefile
set ID=`echo "$install_dir" | sed 's/\//\\\//g'`
'rm' -rf Gmakefile
sed -e "\
s/NV_VER=sed/NV_VER=${NV_VER}/g\
s/NVWISH_VER=sed/NVWISH_VER=${NVWISH_VER}/g\
" Gmakefile.in > Gmakefile

# Done, now make it
echo Configuration complete, use $GMAKE_VER in this directory to make nviz.

exit

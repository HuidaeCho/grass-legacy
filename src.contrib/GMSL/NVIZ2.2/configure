#!/bin/csh
# configure script for nviz

# Revisions:
#  11/11/99 - updated GL path
#  11/26/97 - Modified to work by changing two following variables
#  08/29/99 - small bugfixes
#   2/26/96 - This script created for v1.0
#   5/1/96  - This script modified to work for v2.0

set NV_VER=nviz2.2
set NVWISH_VER=NVWISH2.2
set GMAKE_VER=gmake5

# First print out some introductory instructions
echo This script creates a Gmakefile to build the nviz application.
echo Note that nviz requires Grass5.0 to compile and run correctly.
echo In addition, nviz requires Tcl/Tk 8.0.
echo Note that earlier versions of Tk and Tcl WILL NOT work.
echo
echo Any queries generated by this script will have default settings
echo which are indicated by brackets \(i.e. \[\]\).  You may either enter
echo a value or just press return to use the default setting.
echo
echo -n Press return to continue:
set select=$<

set TOP=$cwd

# The first thing we configure for is the src Gmakefile
# Figure out what version of IRIX we are using
#if ("`uname -s`" != "IRIX") then
#  echo This version of nviz only runs on SGIs running IRIX 
#  exit
#endif

# Look for tcl, tk libraries
set spath=(/lib /usr/lib /usr/local/lib /usr/X11R6/lib)
set libs_reqd=(libtcl8.0 libtk8.0 libX11 libGL )
set def_dirs=(/usr/local/lib /usr/local/lib /usr/local/lib /usr/lib /usr/lib )
set TK_LIB_PATH=''
set index=1
foreach k ($libs_reqd)
  echo -n looking for $k...
  set found=-1
  foreach i ($spath)
    if ("$found" == "-1") then
      foreach j (`'ls' $i`)
        switch ($j)
          case $k*:
            set found=-L$i
            echo $i
            break
            breaksw
        endsw
      end
    endif
  end  
  if ("$found" == "-1") then
    echo can\'t find it
    echo -n Enter the directory where $k is located \[$def_dirs[$index]\]:
    set found=$<
    if ("$found" == "") then
      set found=-L$def_dirs[$index]
    else 
      set found=-L$found
    endif
  endif
  set TK_LIB_PATH="$TK_LIB_PATH $found"
  @ index++
end

# Remove duplicates and set the path
set TK_LIB_PATH=`echo $TK_LIB_PATH | sed -e 's/ /\\n/g'`
set TK_LIB_PATH=`echo $TK_LIB_PATH | sort -u | sed -e 's/\\n/ /g'`
set TK_LIB_PATH="$TK_LIB_PATH -ltk8.0 -ltcl8.0"

# Look for tcl, tk includes
set spath=(/usr/local/include /usr/local/lib/tk8.0/include /usr/local/lib/tcl8.0/include /usr/include /usr/include/X11 /usr/include/gl /usr/X11R6/include /usr/include/GL)
set includes_reqd=(tk.h tcl.h gl.h X.h)
set def_dirs=(/usr/local/include /usr/include /usr/include/X11 /usr/include/GL /usr/include/gl)
set TK_INCLUDE_PATH=''
set index=1
foreach k ($includes_reqd)
  echo -n looking for $k...
  set found=-1
  foreach i ($spath)
    if ("$found" == "-1") then
      foreach j (`'ls' $i`)
        switch ($j)
          case $k:
            set found=-I$i
            echo $i
            break
            breaksw
        endsw
      end
    endif
  end  
  if ("$found" == "-1") then
    echo can\'t find it
    echo -n Enter the directory where $k is located \[$def_dirs[$index]\]:
    set found=$<
    if ("$found" == "") then
      set found=-I$def_dirs[$index]
    else
      set found=-I$found
    endif
  endif
  set TK_INCLUDE_PATH="$TK_INCLUDE_PATH $found"
  @ index++
end

# Remove duplicates and set the path
set TK_INCLUDE_PATH=`echo $TK_INCLUDE_PATH | sed -e 's/ /\\n/g'`
set TK_INCLUDE_PATH=`echo $TK_INCLUDE_PATH | sort -u | sed -e 's/\\n/ /g'`

# Figure out what gmake to use
echo -n Command used to make current version of GRASS \[$GMAKE_VER\]:
set found=$<
if ("$found" != "") then
  set GMAKE_VER=$found
endif
set GISBASE=`$GMAKE_VER config | sed -e '$ n; d'`

# Figure out where to install the nviz scripts
set install_dir=$GISBASE/etc/$NV_VER
echo -n Directory where run-time scripts are located \[$install_dir\]:
set found=$<
if ("$found" != "") then
  set install_dir=$found
endif

# Now create the makefile
echo Creating src makefile...
set SCRIPTS=${install_dir}/scripts
set SCRIPTS=`echo "$SCRIPTS" | sed 's/\//\\\//g'`
set TK_LIB_PATH=`echo "$TK_LIB_PATH" | sed 's/\//\\\//g'`
set TK_INCLUDE_PATH=`echo "$TK_INCLUDE_PATH" | sed 's/\//\\\//g'`
'rm' -rf src/Gmakefile
sed -e "\
s/TK_LIB_PATH=sed/TK_LIB_PATH=${TK_LIB_PATH}/g;\
s/TK_INCLUDE_PATH=sed/TK_INCLUDE_PATH=${TK_INCLUDE_PATH}/g;\
s/SCRIPTS=sed/SCRIPTS=${SCRIPTS}/g\
s/NVWISH_VER=sed/NVWISH_VER=${NVWISH_VER}/g\
" src/Gmakefile.in > src/Gmakefile

# Now figure out how we need to configure the tcl scripts
# set panel_path=${install_dir}/scripts
# set bit_maps=${install_dir}/bitmaps

# Looks good, now lets make the config file in the scripts directory
# echo Configuring nviz scripts...
# set panel_path=`echo "$panel_path" | sed 's/\//\\\//g'`
# set bit_maps=`echo "$bit_maps" | sed 's/\//\\\//g'`
# 'rm' -rf scripts/config.tcl
# sed -e "\
# s/default_panel_path=sed/default_panel_path ${panel_path}/g;\
# s/bit_map_path=sed/bit_map_path ${bit_maps}/g\
# " scripts/config.tcl.in > scripts/config.tcl
#   WBH - don't hard-code these paths

# Finally create the toplevel makefile
echo Creating top-level makefile
set ID=`echo "$install_dir" | sed 's/\//\\\//g'`
'rm' -rf Gmakefile
sed -e "\
s/INSTALL_DIR=sed/INSTALL_DIR=${ID}/g\
s/NV_VER=sed/NV_VER=${NV_VER}/g\
s/NVWISH_VER=sed/NVWISH_VER=${NVWISH_VER}/g\
" Gmakefile.in > Gmakefile

# Done, now make it
echo Configuration complete, use $GMAKE_VER in this directory to make nviz.

exit
